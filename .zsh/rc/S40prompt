#!/usr/bin/zsh

autoload -U colors
colors

autoload -Uz vcs_info
setopt prompt_subst   # required to use $vcs_info_msg_0_ in PROMPT

red="%{$fg[red]%}"
blue="%{$fg[blue]%}"
bblue="%{$fg_bold[blue]%}"
green="%{$fg[green]%}"
magenta="%{$fg[magenta]%}"
cyan="%{$fg[cyan]%}"
yellow="%{$fg[yellow]%}"
default="%{$reset_color%}"

FMT_BRANCH="${green}%b%u%c${default}" # e.g. master¹²
FMT_ACTION="(${blue}%a${default}%)"   # e.g. (rebase-i)
FMT_PATH="%R${}/%S"              # e.g. ~/repo/subdir

# check-for-changes can be kind of expensive, may want to disable for big repos
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' unstagedstr   '¹'  # display ¹ if there are unstaged changes
zstyle ':vcs_info:*' stagedstr     '²'  # display ² if there are staged changes
zstyle ':vcs_info:*' actionformats "${FMT_BRANCH}${FMT_ACTION}//" "${FMT_PATH}"
zstyle ':vcs_info:*' formats       "${FMT_BRANCH}//"              "${FMT_PATH}"
zstyle ':vcs_info:*' nvcsformats   ""                             "%~"
zstyle ':vcs_info:*' enable git svn

# PROMPT in single quotes so that it evaluates ${vcs_info_msg_0_} on display, not on set
PROMPT='${blue}[$cyan%(!.ROOT.%n)$green@$cyan%m$green:$cyan%~$blue]${blue}[${vcs_info_msg_0_}${blue}]%(0#.$red#$default.$default%%) '
RPROMPT="%(?..[$red%?$default])[$cyan%*$default]"

function title () {
    local CMD=$1
    local ARGS=$2

    case $TERM in
	xterm*) ;&
	rxvt*)
        print -nP "\e]2;"
        print -nP "${CMD:+ $CMD $ARGS : }"
        print -nP "%n@%m:%~"
        print -nP "\a"
    ;;
	screen)
        print -nP "\e\"${CMD:-%~}\e\\"
        print -nP "\e_"
        print -nP "#$WINDOW : ${CMD:+ $CMD : }%n@%m:%~"
        print -nP "\e\\"
    ;;
	*)
	    TITLEBAR=''
    ;;
    esac
}

function preexec() {
    local -a cmd
    cmd=(${(z)1})

    title $cmd[1] "$cmd[2,-1]"
}

function precmd() {
    title
    vcs_info
}

