#!/usr/bin/zsh

autoload -U colors
colors

autoload -Uz vcs_info
setopt prompt_subst

red="%{$fg[red]%}"
blue="%{$fg[blue]%}"
bblue="%{$fg_bold[blue]%}"
green="%{$fg[green]%}"
magenta="%{$fg[magenta]%}"
cyan="%{$fg[cyan]%}"
yellow="%{$fg[yellow]%}"
default="%{$reset_color%}"

FMT_BRANCH="${green}%b%u%c${default}" # e.g. masterÂ¹Â²
FMT_ACTION="(${blue}%a${default}%)"   # e.g. (rebase-i)
FMT_PATH="%R${}/%S"              # e.g. ~/repo/subdir

#zstyle ':vcs_info:*' actionformats \
#    '%F{5}(%f%s%F{5})%F{3}-%F{5}[%F{2}%b%F{3}|%F{1}%a%F{5}]%f '
#zstyle ':vcs_info:*' formats       \
#    '%F{5}(%f%s%F{5})%F{3}-%F{5}[%F{2}%b%F{5}]%f '
#zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{1}:%F{3}%r'
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' unstagedstr   '¹'  # display ¹ if there are unstaged changes
zstyle ':vcs_info:*' stagedstr     '²'  # display ² if there are staged changes
zstyle ':vcs_info:*' actionformats "${FMT_BRANCH}${FMT_ACTION}//" "${FMT_PATH}"
zstyle ':vcs_info:*' formats       "${FMT_BRANCH}//"              "${FMT_PATH}"
zstyle ':vcs_info:*' nvcsformats   ""                             "%~"

PROMPT='${blue}[$cyan%(!.ROOT.%n)$green@$cyan%m$green:$cyan%~$blue]${blue}[${vcs_info_msg_0_}${blue}]%(0#.$red#$default.$default%%) '
RPROMPT="%(?..[$red%?$default])[$cyan%*$default]"

function title () {
    local CMD=$1
    local ARGS=$2

    case $TERM in
	xterm*) ;&
	rxvt*)
        print -nP "\e]2;"
        print -nP "${CMD:+ $CMD $ARGS : }"
        print -nP "%n@%m:%~"
        print -nP "\a"
    ;;
	screen)
        print -nP "\e\"${CMD:-%~}\e\\"
        print -nP "\e_"
        print -nP "#$WINDOW : ${CMD:+ $CMD : }%n@%m:%~"
        print -nP "\e\\"
    ;;
	*)
	    TITLEBAR=''
    ;;
    esac
}

function preexec() {
    local -a cmd
    cmd=(${(z)1})

    title $cmd[1] "$cmd[2,-1]"
}

function precmd() {
    title
    vcs_info
}

